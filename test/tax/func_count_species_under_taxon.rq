#!/usr/bin/env spang2

# @title Count species under taxon
# @endpoint test
# @param taxid=40674

_:countSpecies(?count, ?taxon) {
  {
    SELECT (COUNT(DISTINCT ?species) AS ?count) ?taxon
    WHERE {
      ?species taxon:rank taxon:Species ;
          rdfs:subClassOf+ ?taxon .
    }
  }
}

SELECT ?order ?order_name ?order_count ?family ?family_name ?family_count ?species ?species_name
WHERE {
  ?species taxon:rank taxon:Species ;
      rdfs:label ?species_name ;
      rdfs:subClassOf+ taxid:{{taxid}} ;
      rdfs:subClassOf+ ?family ;
      rdfs:subClassOf+ ?order .
  ?family taxon:rank taxon:Family ;
      rdfs:label ?family_name .
  ?order taxon:rank taxon:Order ;
      rdfs:label ?order_name .
  _:countSpecies(?family_count, ?family)
  _:countSpecies(?order_count, ?order)
}
ORDER BY DESC(?order_count) ?order_name DESC(?family_count) ?family_name ?species_name
